AC_PREREQ(2.54)
# Largely based on cairo's configure.in

m4_define(clip_version_major, 1)
m4_define(clip_version_minor, 2)
m4_define(clip_version_micro, 17)

AC_INIT([clip],
      clip_version_major.clip_version_minor.clip_version_micro,
      [clipos@ssi.gouv.fr])
AC_CONFIG_SRCDIR(src/clip.h)
AC_CONFIG_MACRO_DIR([m4])
AM_INIT_AUTOMAKE([1.10])

dnl ===========================================================================

VERSION_INFO=clip_version_major:clip_version_micro:0
AC_SUBST(VERSION_INFO)

CLIP_VERSION_MAJOR=clip_version_major()
CLIP_VERSION_MINOR=clip_version_minor()
CLIP_VERSION_MICRO=clip_version_micro()
AC_SUBST(CLIP_VERSION_MAJOR)
AC_SUBST(CLIP_VERSION_MINOR)
AC_SUBST(CLIP_VERSION_MICRO)

dnl AM_MAINTAINER_MODE

AC_PROG_CC
AM_PROG_LIBTOOL
AC_STDC_HEADERS
AC_PROG_INSTALL

PKG_PROG_PKG_CONFIG

dnl ===========================================================================
dnl === Local macros
dnl ===========================================================================

# _CHECK_FUNCS_WITH_FLAGS(FUNCTION..., CFLAGS, LIBS 
#                         [, ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND]])
# Like AC_CHECK_FUNCS but with additional CFLAGS and LIBS
# --------------------------------------------------------------------
AC_DEFUN([_CHECK_FUNCS_WITH_FLAGS],
[ 
  save_cflags="$CFLAGS"
  save_libs="$LIBS"
  CFLAGS="$CFLAGS $2"
  LIBS="$LIBS $3"
  AC_CHECK_FUNCS($1, $4, $5)
  CFLAGS="$save_cflags"
  LIBS="$save_libs"
])

dnl ===========================================================================

AC_CHECK_HEADERS([sys/types.h sys/socket.h unistd.h])
AC_CHECK_TYPES([uid_t, gid_t])
	

dnl ===========================================================================

AC_ARG_ENABLE(html-doc, 
	[  --enable-html-doc        create and install html documentation (default=no)],
		enable_html_doc=$enableval, enable_html_doc=no)
AC_MSG_CHECKING(if html documentation should be created)
if test "x$enable_html_doc" = xyes; then
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(HTML_DOC, test "x$enable_html_doc" = xyes)

dnl ===========================================================================

AC_CHECK_HEADERS([stdint.h inttypes.h sys/int_types.h])
AC_CHECK_TYPES([uint64_t, uint128_t])

dnl Use lots of warning flags with GCC

WARN_CFLAGS=""

if test "x$GCC" = "xyes"; then
	WARN_CFLAGS="-Wall -Wextra -Werror \
	-Wstrict-prototypes -Wmissing-prototypes \
	-Wcast-qual -Wcast-align -Wpointer-arith \
	-Wwrite-strings -Wconversion \
	-Wnested-externs"
fi

usrlibdir="/usr${PREFIX#/usr}/lib"
AC_SUBST(usrlibdir)

CLIP_CFLAGS="$CLIP_CFLAGS $WARN_CFLAGS"

AC_SUBST(CLIP_CFLAGS)

dnl ===========================================================================

AC_OUTPUT([
clip.pc
Makefile
src/Makefile
docs/Makefile
])
